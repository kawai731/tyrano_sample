*start

[title name="巨大数クエスト"]
[hidemenubutton]
[wait time=200]
[freeimage layer="base"]

[bg storage=michi.png time=500]
「巨大数クエスト」[l][r]

君は見習いの冒険者。[l][r]

魔王城を目指して，敵を倒そう！[l][r]

敵のHPよりも大きいダメージを与えたら，敵は倒れる，簡単だろう？[l][r]

お！早速スライムが現れたようだ！[l][cm]
[bg storage=スライム.png time=500]
敵のHPを見てみよう，どれどれ[l][r]

ふむ，スライムのHPは1𥝱(じょ)だ．[l][r]

まさに序盤，といった控えめなHPだな！[l][r]

さあ，どの攻撃をする？[l][r]

[link target=*over1] →たいあたり(1億ダメージ) [endlink][r]
[link target=*over1] →はたく(ルービックキューブの組み合わせ数ダメージ) [endlink][r]
[link target=*over1] →ひっかく(アボガドロ数ダメージ) [endlink][r]
[link target=*tag_run1] →踏む(地球の質量(kg)ダメージ) [endlink][r]
[s]

*over1

[cm]

[bg storage=sleep.jpg time=500]

まずい！相手のHPを削り切れなかった！[l][r]
1𥝱は10^24だ．それより大きなダメージを出そう．[r]

【 GAME OVER 】[l][cm]

[jump target=*start]

*tag_run1

[bg storage=michi.png time=500]

[cm]
テレレレッテテッテー！レベルが100極上がった！[l][r]
やるじゃないか．1𥝱は10^24，地球の質量は5.97×10^24だな．[l][r]
危ない危ない，スライムのHPの6倍程度しかダメージを出せていないじゃないか．[r][l]
ルービックキューブの組み合わせは4330京，アボガドロ数は6020垓で全然小さかったな．[r][l]
まあ問題ない，レベルも少し上がったようだし，ちょっとはダメージも出せるんじゃないか？[l][r]
さあ，次の敵も現れたぞ！[l][cm]

[bg storage=teki2.png time=500]
次の敵は変異スライムか，普通のスライムよりは強いが，大したことはない．[l][r]
HPもたったの1グーゴルだ．ちゃちゃっと倒そう！[l][r]

[link target=*over2] →とっしん(54枚のトランプのシャッフルパターン数，54!) [endlink][r]
[link target=*run2] →ビンタ(チェスの可能な差し手の総数，シャノン数) [endlink][r]
[link target=*over2] →きりさく(観測可能な宇宙に存在する全原子数) [endlink][r]
[link target=*over2] →踏み潰す(観測史上最大のブラックホールTON618(660億太陽質量)が消滅するまでにかかる年数) [endlink][r]
[s]

*over2

[cm]
まずい！相手のHPを削り切れなかった！[l][r]
1グーゴルは10^100だ．それより大きなダメージを出そう．[r]

【 GAME OVER 】[l][cm]

[jump target=*tag_run1]

*run2
[cm]

[bg storage=yamamichi.jpg time=500]
テレレレッテテッテー!レベルが1阿僧祇上がった![l][r]
シャノン数は10^120程度のオーダーだ．グーゴルよりも少し大きいな，まだまだひよっこ冒険者といったところか[l][r]
トランプの組み合わせはおよそ200無量大数，観測可能な宇宙の原子数は約10^80，TON618の消滅年数は6×10^99と，少し足りなかったな．[l][r]


[cm]
さて，ここは迷いの森か．[l][r]
[cm]

[bg storage=teki3.png time=500]
うおっ！なかなか強そうな敵が出てきてしまったな．[l][r]
HPは倍精度浮動小数点で表せる最大値だ．[l][r]

ただしお前も新しいわざを覚えたな．さてどの攻撃にする?[l][r]
[link target=*run3] →メラ(世界最大のルービックキューブ(33×33×33)の組み合わせ数) [endlink][r]
[link target=*over3] →ボム(囲碁の盤面パターン数) [endlink][r]
[link target=*over3] →水鉄砲(観測可能な宇宙に存在するすべての核子が崩壊するまでにかかる年数) [endlink][r]
[s]

*over3
[cm]
まずい！相手のHPを削り切れなかった！[l][r]
倍精度浮動小数点で表せる最大値は約1.8×10^308だ．エクセルで表示可能な数もそれに対応して9.999...×10^307だぞ[r]
それより大きなダメージを出そう．[l][r]

【 GAME OVER 】[l][cm]

[jump target=*run2]

*run3
[cm]

[bg storage=yukiyama.png time=500]
テレレレッテテッテー!レベルが10^1000上がった![l][r]
世界最大のルービックキューブの組み合わせは10^4099だ．余裕をもって倒せたな[l][r]
囲碁の盤面は10^170，核子崩壊までは約10^200年だな．[l][r]
これからの戦いでは使い物にならなそうだ[l][r]

[cm]
森を抜けて，豪雪地帯の山脈に入った．敵のHPも指数で表すのが大変になってくる頃合いだな[l][r]
[cm]

[bg storage=teki5.png time=500]
次の敵はこいつか．今までのモンスターとは次元の違う強さだ，気をつけろ![l][r]
HPはグーゴルプレックスだ．[l][r]

さっきのわざが強化されているはずだ．さてどの攻撃にする?[l][r]
[link target=*run4] →フレア(宇宙のエントロピーが最大に達し，完全な均一状態(熱的死)を迎える年数) [endlink][r]
[link target=*over4] →ボン・バーナ(不可説不可説転) [endlink][r]
[link target=*over4] →アクアキャノン(量子揺らぎにより真空中に人間のような記憶や知性を持った単一の脳(ボルツマン脳)発生までの年数) [endlink][r]
[s]

*over4
[cm]
まずい！相手のHPを削り切れなかった！[l][r]
グーゴルプレックスは1の後に0がグーゴル個並んだ数，10^10^100だ．[r]
それより大きなダメージを出そう．[l][r]

【 GAME OVER 】[l][cm]

[jump target=*run3]

*run4
[cm]

[bg storage=maouzyou.jpeg time=500]
テレレレッテテッテー![l][r]
熱的死までの年数は10^10^120だな[l][r]
不可説不可説転は10^10^36(10の1正乗)，ボルツマン脳発生までは約10^10^50年だな．[l][r]
さあどんどんいこう[l][r]

[cm]
いよいよ魔王城が見えてきた．最終決戦も近いかもしれないな．[l][r]
[cm]

[bg storage=teki4.png time=500]
次の敵はこいつか．魔王幹部，といったところか．こいつのHPは,[l][r]
なにっ!3↑↑↑3だとっ？！こんなもの，宇宙がいくつあっても足りないじゃないか[l][r]

こいつを倒せるわざなどあるのか?[l][r]
[link target=*over5] →マグマショット(量子トンネル効果により新たなビッグバンが起き，現宇宙と同じ物理法則をもつ新たな宇宙が誕生するまでの年数) [endlink][r]
[link target=*over5] →エクスプロージョン(10↑10↑10↑10) [endlink][r]
[link target=*run5] →津波(3↑↑↑↑↑3) [endlink][r]
[s]

*over5
[cm]
まずい！相手のHPを削り切れなかった！[l][r]
3↑↑↑3は3^3^3^3^3^3・・・と3の指数タワーが3^3^3(約7.6兆)段重なった数だ[r]
それより大きなダメージを出そう．[l][r]

【 GAME OVER 】[l][cm]

[jump target=*run4]

*run5
[cm]

[bg storage=saigonoma.jpg time=500]
テレレレッテテッテー![l][r]
矢印表記で数を大きくするには，矢印の数を増やすのが賢明だな．[l][r]
新宇宙発生までは10^10^10^56，10↑10↑10↑10はそのまま10^10^10^10だからそこまで大きくないな．[l][r]
これでもう他の敵はいない[l][r]

[cm]
いざ，魔王へ挑もうではないか[l][r]
[cm]

[bg storage=lastboss.png time=500]
さすがは魔王，とんでもないHPをしていやがる[l][r]
こいつのHPはグラハム数．数学の証明で用いられた最大の数としてギネスに記録されている．[l][r]
さっきの矢印表記ですら表現が困難な凄まじい数だ．[l][r]

さあ，こいつを倒して世界を救おう[l][r]
[link target=*run6] →霊子崩壊/ディス・インティグレーション(TREE(3)) [endlink][r]
[link target=*run6] →崩星咆哮砲/メテオリックバースト(ローダー数) [endlink][r]
[link target=*run6] →竜星爆炎覇/ドラゴ・ノヴァ(ラヨ数) [endlink][r]
[s]

*run6
[cm]
[bg storage=saigonoma.jpg time=500]
魔王はちりも残さないほどバラバラになり消滅した[l][r]
まさか，どのわざでもグラハム数を超える程成長しているとは．．．[l][r]
まさしく君は，勇者，だな[l][r]
[cm]
巨大数クエスト　完[l]

[cm]

[jump target=*start]
